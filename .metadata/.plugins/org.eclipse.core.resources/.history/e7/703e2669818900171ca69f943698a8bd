RobotSystem rover_wall_e_sys
                     
Event usercmd : usercmd(robotgui(CMD)) 	//(FORWARD button) CMD w(low)
Event local_start : start
Event local_alarm : alarm

Event sonar : sonar(SONARID,TARGETNAME,DISTANCE) //EVENT ENVIRONMENT SONAR1&2
Event local_sonar2Support : sonar2Support(TARGETNAME,DISTANCE)
Event local_goalEvent : goalEvent

Event obstacle : obstacle(DISTANCE)      
Event noMoreObstacle : noMoreObstacle(DISTANCE)

Dispatch daValue : daValue(DA)



Context  ctxRobot  ip [ host="localhost" port=8070 ] -httpserver
Context  ctxSonar1  ip [ host="192.168.43.74" port=8100 ]
Context  ctxSonar2  ip [ host="192.168.43.254" port=8090 ]  

QActor sonar1 context ctxSonar1 {
	
	Rules {
		testNear(Distance):-retract(actorOpDone(OP, Distance)), eval(lt, Distance, 90).
	}
	
	Plan init normal
			println("Sonar1 STARTS");
			actorOp startSonarProcess;
			switchToPlan work;
			println("Sonar1 ENDS")
			
	Plan work 
			delay time(50);
			actorOp getDistanceFromSonar;
			[ !? testNear(Distance) ] emit sonar:sonar(sonar1, wall_e, Distance);
			repeatPlan
	
}
 
QActor sonar2 context ctxSonar2 {
	Rules {
		testNear(Distance):-retract(actorOpDone(OP, Distance)), eval(lt, Distance, 90).
	}
	
	Plan init normal
			println("sonar STARTS");
			actorOp startSonarProcess;
			switchToPlan work;
			println("sonar ENDS")
			
	Plan work 
			delay time(50);
			actorOp getDistanceFromSonar;
			[ !? testNear(Distance) ] emit sonar:sonar(sonar2, wall_e, Distance);
			repeatPlan
}

QActor sonarsupport context ctxRobot {
	
	Plan init normal
			println("Sonar Support STARTS");
			switchToPlan work;
			println("Sonar Support ENDS")
			
	Plan work
			sense time(60000) sonar -> handleSonar;
			repeatPlan
			
	Plan handleSonar
			onEvent sonar : sonar(sonar1, wall_e, D) -> switchToPlan handleSonar1;
			onEvent sonar : sonar(sonar2, wall_e, D) -> switchToPlan handleSonar2;
			resumeLastPlan
	
	Plan handleSonar1
			[ !? da(DA) ] resumeLastPlan else switchToPlan setDa;
			resumeLastPlan
	
	Plan handleSonar2
			onEvent sonar : sonar(sonar2, wall_e, D) -> emit local_sonar2Support : sonar2Support(wall_e, D);
		 	onEvent sonar : sonar(sonar2, wall_e, D) -> actorOp goalTest(D);
		 	[ !? actorOpDone(OP, true) ] println("[SUPPORT] emitting goalEvent");
   			[ ?? actorOpDone(OP, true) ] emit local_goalEvent : goalEvent;
   			resumeLastPlan
   	
   	Plan setDa
   			onEvent sonar : sonar(sonar1, wall_e, D) -> addRule da(D);
   			onEvent sonar : sonar(sonar1, wall_e, D) -> actorOp setDa(D);
   			onEvent sonar : sonar(sonar1, wall_e, D) -> forward rover -m daValue : daValue(D);
   			[ !? da(DA) ] println(daSettedAt(DA));
   			resumeLastPlan
} 

QActor usercmdsupport context ctxRobot {
	
	Plan init normal
			println("UserCmd Support STARTS");
			switchToPlan work;
			println("UserCmd Support ENDS")
			
	Plan work
			sense time(60000) usercmd -> handleUserCmd;
			repeatPlan
			
	Plan handleUserCmd
			onEvent usercmd : usercmd(robotgui(start)) -> emit local_start : start;
			onEvent usercmd : usercmd(robotgui(alarm)) -> emit local_alarm : alarm;
			resumeLastPlan
}

Robot wall_e QActor rover context ctxRobot {
   	
	Plan init normal
 			println("Rover STARTS");           
  			//---------UNITY----------
  			//Metodo 1 unity
  			//actorOp workWithUnity("127.0.0.1");//connects to port 6000 192.168.43.229"
			//Metodo 2 unity
			//actorOp initUnityConnection("192.168.0.3");
			//actorOp createRobotUnity;
			//---------UNITY----------
			switchToPlan config;
      		switchToPlan standby;  
			delay time(6000000); 
   			println("rover ENDS")                      

	Plan config
			actorOp setAttemptRight(0);
			actorOp getRotationTimeRight;
			[ ?? actorOpDone(OP, RTR) ] addRule rotationTimeRight(RTR);
			actorOp getRotationTimeLeft;
			[ ?? actorOpDone(OP, RTL) ] addRule rotationTimeLeft(RTL);
			actorOp getSearchTimeForward;
			[ ?? actorOpDone(OP, STF) ] addRule searchTimeForward(STF);
			actorOp getSenseTimeFixedObstacle;
			[ ?? actorOpDone(OP, STFO) ] addRule senseTimeFixedObstacle(STFO);
			actorOp getFinalPointTollerance;
			[ ?? actorOpDone(OP, FPT) ] addRule finalPointTollerance(FPT);			
			//Waiting for DA
			receiveMsg time(10000);
			onMsg daValue : daValue(DA) -> addRule da(DA);
			onMsg daValue : daValue(DA) -> actorOp setDa(DA);
			[ !? da(X) ] resumeLastPlan else repeatPlan
			
	Plan standby  
			println("rover waits to start.");    
			sense time(10000) local_start -> handleStart;
			repeatPlan 	

	Plan handleStart  
   			switchToPlan goForward;
   			resumeLastPlan
   			
	Plan handleAlarm 
			robotStop speed(0) time(0); 
			sense time(10000) local_start -> continue; //Da testare se la else funziona nella sense.
			onEvent local_start : start -> println("Resuming From Alarm") else repeatPlan
			
	Plan goForward 
   			robotForward speed(100) time(60000) react event local_sonar2Support -> handleSonar2Support 
	   											or    event obstacle -> handleObstacle
	   											or 	  event local_alarm -> handleAlarm;
			repeatPlan
	   											
	Plan handleSonar2Support
   			[ ?? goLeft ] println("GoLeft Rule Deleted!");
   			onEvent local_sonar2Support : sonar2Support(wall_e, D) -> robotStop speed(0) time(0);
   			onEvent local_sonar2Support : sonar2Support(wall_e, D) -> switchToPlan goalLine;
   			resumeLastPlan
   			
   	Plan goalLine
   			println("I'm on the GoalLine.");
   			onEvent local_sonar2Support : sonar2Support(wall_e, D) -> actorOp chooseDirection(D);
   			[ ?? actorOpDone(OP, 0) ] switchToPlan goal;
   			[ ?? actorOpDone(OP, 1) ] switchToPlan turnRight90;
   			[ ?? actorOpDone(OP, 2) ] switchToPlan turnLeft90;
   			switchToPlan reachFinalPosition
   	
   	Plan reachFinalPosition
   			robotForward speed(100) time(20000) react event local_goalEvent -> goal 
   												or 	  event local_alarm -> handleAlarm;
   			repeatPlan
   			
   	Plan goal
   			println("Point B reached!!");
   			robotRight speed(100) time(3000);
   			actorOp exit
   			
   	Plan handleObstacle
   			onEvent obstacle : obstacle(DISTANCE) -> robotStop speed(0) time(0);
   			switchToPlan typeOfObstacle
   			
   	Plan typeOfObstacle
   			[ !? senseTimeFixedObstacle(T) ] sense time (T) noMoreObstacle -> mobileObstacle 
   												react event local_alarm -> handleAlarm;
   			switchToPlan fixedObstacle
   			
   	Plan mobileObstacle
   			switchToPlan goForward	
   			
   	Plan turnRight90
   			[ !? rotationTimeRight(T) ] robotRight speed(100) time(T) react event local_alarm -> handleAlarm;
   			resumeLastPlan
   			
   	Plan turnLeft90
   			[ !? rotationTimeLeft(T) ] robotLeft speed(100) time(T) react event local_alarm -> handleAlarm;
   			resumeLastPlan
   			
   	Plan fixedObstacle
   			[ !? goLeft ] switchToPlan findTheDoorLeft;
	   		switchToPlan findTheDoorRight
	   		
   	Plan findTheDoorRight
   			actorOp incAttemptRight;
   			switchToPlan turnRight90;
   			[ !? searchTimeForward(T) ] robotForward speed(100) time(T) react event obstacle -> deadEndRight 
   																		or 	  event local_alarm -> handleAlarm;
   			switchToPlan turnLeft90;
   			sense time (500) obstacle -> handleObstacle;
   			switchToPlan goForward
   			
   	Plan findTheDoorLeft
   	   		switchToPlan turnLeft90;
   			[ !? searchTimeForward(T) ] robotForward speed(100) time(T) react event obstacle -> deadEndLeft
   																		or	  event local_alarm -> handleAlarm;
   			switchToPlan turnRight90;
   			sense time (500) obstacle -> handleObstacle;
   			switchToPlan goForward
   			
   	Plan deadEndRight
   			addRule goLeft;
   			switchToPlan turnLeft90;
   			delay time(1000) react event local_alarm -> handleAlarm;
   			switchToPlan turnLeft90;
   			actorOp getAttemptRightTotalTime;
   			[ !? actorOpDone(OP, Result) ] println(risultatoTotalTime(Result));
   			[ ?? actorOpDone(OP, Result) ] robotForward speed(100) time(Result) react event local_alarm -> handleAlarm;
   			switchToPlan turnRight90;
   			delay time(1000);
   			switchToPlan goForward
   			
   	Plan deadEndLeft
   			println("Cannot find a door. Terminate.");
   			actorOp setAttemptRight(0);
   			[ ?? goLeft ] println("GoLeft Rule Deleted!");
   			switchToPlan turnRight90;
   			switchToPlan turnLeft90;
   			switchToPlan turnLeft90;
   			switchToPlan turnRight90;
   			switchToPlan turnRight90;
   			switchToPlan turnLeft90;
   			switchToPlan standby
   					   			 			
}